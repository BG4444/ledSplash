.include "tn15def.inc"
//Тестовый пример - прямоугольные импульсы с частотой 6,10Гц. (1.6МГц/(1024*256)

//Таблица переходов по прерываниям, используются только сброс и таймер-0
//Прочие прерывания блокированы
//поэтому мы сразу разместим там код

//подготовка таймера 0 - выбор источника тактирования
	//источник - тактовый генератор 1.6 МГц с делителем на 1024
	//дает инкремент таймера каждый 1024 такт
	//выполняется путем записи в регистр TCCR0 комбинации флагов CS00 и CS02
	//даташит, страница 27, таблица 9
	ldi r31,(1<<cs00 ) | (1<<cs02)
	out tccr0,r31
	
	//Разрешение обработки прерывания по переполнению таймера 0
	//выполняется путем записи в регистр TIMSK флага TOIE0
	//даташит, страница 20
	ldi r31,1<<toie0
	out timsk,r31
	//перепрыгиваем обработчик прерывания при переполнении таймера 0
	rjmp reset


////////////////////////////////////////////////////////
//обработчик прерывания от по переполнению таймера 0, вызывается с частотой около 6Гц
	com r31 			//инверсия битов r31
	out portb,r31		//выдача значения порт - либо высокий уровень, либо низкий
	reti				//возврат из прерывания
////////////////////////////////////////////////////////


//обработчик сброса (продолжение)
reset:
	//запись в MCUCR флага разрешения перехода в спящий режим
	ldi r31,(1<<SE)
	out mcucr,r31

	//разрешение обработки прерываний
	sei

	//запись в регистр битовой маски, которая при работе программы будет инвертироваться
	//при переполнении таймера и выдаваться в порт B
	ldi r31,0b100
	//переключение режима порта B, пин 2 (счет с 0) на вывод push-pull
	//даташит, страница 51
	out ddrb,r31

	//бесконечный цикл сна
	lp:		
		sleep	
		rjmp lp	
	
